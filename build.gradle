plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.lgatodu47'
version "${version}" + (snapshot == "true" ? "-SNAPSHOT" : "")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

configurations {
    exports {
        canBeConsumed = false
        canBeResolved = true
    }
    implementation.extendsFrom(exports)
}

dependencies {
    exports group: 'com.google.code.gson', name: 'gson', version: "${gson_version}"
}

jar {
    manifest {
        attributes(
                "Main-class": "io.github.lgatodu47.meowrapper.MeoWrapper"
        )
    }
}

task fatJar(type: Jar) {
    classifier 'cmd'
    group 'build'

    manifest = jar.manifest

    from { configurations.exports.collect {it.isDirectory() ? it : zipTree(it)} }

    with jar
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        meowrapper(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'MeoWrapper'
                description = 'A simple minecraft launchwrapper'
                url = 'https://github.com/LGatodu47/MeoWrapper'
                licenses {
                    license {
                        name = 'MIT License'
                        url = "https://www.opensource.org/licenses/mit-license.php"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/LGatodu47/MeoWrapper.git"
                    developerConnection = "scm:git:ssh://github.com/LGatodu47/MeoWrapper.git"
                    url = "https://github.com/LGatodu47/MeoWrapper"
                }

                developers {
                    developer {
                        id = 'lgatodu47'
                        name = 'LGatodu47'
                        email = 'driph.v450@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "Local"
            url layout.buildDirectory.dir("repo")
        }
        maven {
            name = "OSSRH"
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'

            credentials {
                username System.getProperty("meowrapper.publish.username")
                password System.getProperty("meowrapper.publish.password")
            }
        }
    }
}

signing {
    sign publishing.publications.meowrapper
}
