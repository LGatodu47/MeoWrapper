plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.lgatodu47'
version "${version}" + (snapshot == "true" ? "-SNAPSHOT" : "")

repositories {
    mavenCentral()

    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '/[organisation]/[module]/releases/download/[revision]/[module].[ext]'
        }

        metadataSources {
            artifact()
        }
    }
}

configurations {
    agent
    exports {
        canBeConsumed = false
        canBeResolved = true
    }
    implementation.extendsFrom(exports, agent)
}

dependencies {
    agent "CGJennings:jar-loader:${agent_version}@jar"
    exports 'net.bytebuddy:byte-buddy-agent:1.12.8'
    exports group: 'com.google.code.gson', name: 'gson', version: "${gson_version}"
}

jar {
    manifest {
        attributes(
                "Main-class": "fr.lgatodu47.meowrapper.MeoWrapper",
                "Premain-Class": "ca.cgjennings.jvm.JarLoader",
                "Agent-Class": "ca.cgjennings.jvm.JarLoader"
        )
    }

    from { configurations.agent.collect { it.isDirectory() ? it : zipTree(it) } }
}

task fatJar(type: Jar) {
    classifier 'fatJar'
    group 'build'

    manifest {
        attributes(
                "Main-class": "fr.lgatodu47.meowrapper.MeoWrapper",
                "Premain-Class": "ca.cgjennings.jvm.JarLoader",
                "Agent-Class": "ca.cgjennings.jvm.JarLoader"
        )
    }

    from { configurations.exports.collect {it.isDirectory() ? it : zipTree(it)} } {
        exclude "META-INF/versions/9/module-info.class"
    }

    with jar;
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

/*signing {
    sign configurations.archives
}*/

publishing {
    publications {
        meowrapper(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'MeoWrapper'
                description = 'A simple minecraft launchwrapper'
                url = 'https://github.com/LGatodu47/MeoWrapper'
                licenses {
                    license {
                        name = 'MIT License'
                        url = "https://www.opensource.org/licenses/mit-license.php"
                    }
                }
            }

            pom.withXml {
                var pomNode = asNode();
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'jar-loader'
                }.each() {
                    it.parent().remove(it);
                }
            }
        }
    }
    repositories {
        maven {
            name = "Local"
            url layout.buildDirectory.dir("repo")
        }
        maven {
            name = "OSSRH"
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'

            credentials {
                username System.getProperty("meowrapper.publish.username")
                password System.getProperty("meowrapper.publish.password")
            }
        }
    }
}
